
plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'signing'
    id "com.palantir.git-version" version "0.12.3"
}
Properties properties = new Properties()
def propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    properties.load(propertiesFile.newDataInputStream())
}


group 'com.github.ekimeel'
version gitVersion()
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
sourceCompatibility = 1.8


task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}


repositories {
    mavenCentral()
    maven {
        url "https://maven.pkg.github.com/ekimeel/*"
        credentials {
            username = properties.get("github.actor") ?: System.getenv("GITHUB_ACTOR")
            password = properties.get("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    /* kind */

    compile group: 'com.google.guava', name: 'guava', version: '30.1-jre'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

publishing {
    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = properties.get("ossrh.username") ?: System.getenv("OSSRH_USERNAME")
                password = properties.get("ossrh.password") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) { publication ->
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                groupId = 'com.github.ekimeel'
                name = 'kind-table'
                description = 'A simple to use but, robust table data struction with fast functions, pretty output and extensable tools.'
                url = 'https://github.com/ekimeel/kind-table'
                developers {
                    developer {
                        id = 'ekimeel'
                        name = 'Michael Lee'
                        email = 'michaeljlee0@gmail.com'
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:ekimeel/kind-table.git'
                    developerConnection = 'scm:git:git@github.com:ekimeel/kind-table.git'
                    url = 'https://github.com/ekimeel/kind-table'
                }
            }
        }
    }
}